//file:noinspection GroovyAssignabilityCheck
//file:noinspection GrUnresolvedAccess
//file:noinspection GroovyUnusedAssignment
plugins {
    id 'java'
    id 'java-library'
    id 'idea'
    id 'jacoco'
    id 'maven-publish'
    id 'com.github.spotbugs' version '4.7.9'
    id 'io.freefair.lombok' version "6.2.0"
}

group 'com.github.qky666'
version '0.3.2'


sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.8.1'
}

dependencies {
    // https://mvnrepository.com/artifact/com.codeborne/selenide
    implementation "com.codeborne:selenide:6.0.3"

    // https://mvnrepository.com/artifact/org.slf4j/slf4j-simple
    testImplementation "org.slf4j:slf4j-simple:1.7.32"

    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-engine
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

test {
    useJUnitPlatform()
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Selenide POM'
                description = 'Selenide Pom = POM utilities for Selenide'
                url = 'https://github.com/qky666/selenide-pom'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'qky666'
                        name = 'José Torrecilla Álvarez'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:qky666/selenide-pom.git'
                    developerConnection = 'scm:git@github.com:qky666/selenide-pom.git'
                    url = 'https://github.com/qky666/selenide-pom'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'GitHubPackages'
            url = 'https://maven.pkg.github.com/qky666/selenide-pom'
            credentials {
                username = project.findProperty("gpr.user")
                password = project.findProperty("gpr.key")
            }
        }
    }
}
