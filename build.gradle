//file:noinspection GroovyAssignabilityCheck
//file:noinspection GrUnresolvedAccess
//file:noinspection GroovyUnusedAssignment
plugins {
    id 'java'
    id 'java-library'
    id 'idea'
    id 'jacoco'
    id 'maven-publish'
    id 'com.github.spotbugs' version '4.7.3'
}

group 'com.github.qky666'
version '0.1.5'


sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

//ext {
//    junitVersion = '5.7.2'
//    selenideVersion = '5.24.0'
//    slf4jSimpleVersion = '1.7.32'
//}

dependencies {
    // https://mvnrepository.com/artifact/com.codeborne/selenide
    api "com.codeborne:selenide:5.24.2"
    // implementation "com.codeborne:selenide:$selenideVersion"

    // https://mvnrepository.com/artifact/org.slf4j/slf4j-simple
    testImplementation "org.slf4j:slf4j-simple:1.7.32"
    // testImplementation "org.slf4j:slf4j-simple:$slf4jSimpleVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.2"
}

test {
    useJUnitPlatform()
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Selenide POM'
                description = 'Selenide Pom = POM utilities for Selenide'
                url = 'https://github.com/qky666/selenide-pom'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'qky666'
                        name = 'José Torrecilla Álvarez'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:qky666/selenide-pom.git'
                    developerConnection = 'scm:git@github.com:qky666/selenide-pom.git'
                    url = 'https://github.com/qky666/selenide-pom'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'GitHubPackages'
            url = 'https://maven.pkg.github.com/qky666/selenide-pom'
            credentials {
                username = project.findProperty("gpr.user")
                password = project.findProperty("gpr.key")
            }
        }
    }
}
