//file:noinspection GroovyAssignabilityCheck
plugins {
    id 'java'
    id 'java-library'
    id 'idea'
    id 'jacoco'
    id 'maven-publish'
    id 'com.github.spotbugs' version '4.7.3'
}

group 'es.qky'
version '1.0.0'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.7.2'
    selenideVersion = '5.24.0'
    slf4jSimpleVersion = '1.7.32'
}

dependencies {
    // https://mvnrepository.com/artifact/com.codeborne/selenide
    implementation "com.codeborne:selenide:$selenideVersion"

    // https://mvnrepository.com/artifact/org.slf4j/slf4j-simple
    testImplementation "org.slf4j:slf4j-simple:$slf4jSimpleVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Selenide POM'
                description = 'Selenide Pom = POM utilities for Selenide'
                url = 'https://github.com/qky666/selenide-pom'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'qky666'
                        name = 'José Torrecilla Álvarez'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:qky666/selenide-pom.git'
                    developerConnection = 'scm:git@github.com:qky666/selenide-pom.git'
                    url = 'https://github.com/qky666/selenide-pom'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'GitHubPackages'
            url = 'https://maven.pkg.github.com/qky666/selenide-pom'
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
